import com.terrafolio.gradle.plugins.jenkins.JenkinsPlugin;

buildscript {
    repositories {
        jcenter { url "http://jcenter.bintray.com/" }
    }
    // FG, 2016-06-06: newest version is 1.3.2, but it has missing dependencies
    dependencies { classpath "com.terrafolio:gradle-jenkins-plugin:1.2.3" }
}

// FG, 2016-06-06: in plugin-scripts you can not (yet) use the plugin id, you have to use the actual type
apply plugin: JenkinsPlugin

/*
 * NOTE:
 * we depend on mercurial.gradle - make sure you load it too!
 */
jenkins {
    servers {
        ci {
            url project.hasProperty("jenkinsServer") ? project.property("jenkinsServer") : ""
            username project.hasProperty("jenkinsUsername") ? project.property("jenkinsUsername") : ""
            password project.hasProperty("jenkinsPassword") ? project.property("jenkinsPassword") : ""
            secure true // optional
        }
    }

    templates {
        build { xml file('jenkins-config-template.xml') }
    }

    if (ext.has('hgBranch') == false) {
        throw new RuntimeException("include mercurial.gradle for jenkins.mercurial to work")
    }

    def branchName = ext.hgBranch();
    jobs {
        "build_${branchName}" {
            server servers.ci
            definition {
                name "${project.name}-${branchName.replaceAll('[ /]', '_')}" //optional, no slashes, NO SPACED GODAMMIT
                xml templates.build.override { projectXml ->
                    projectXml.scm.revision = branchName
                }
            }
        }
    }
}